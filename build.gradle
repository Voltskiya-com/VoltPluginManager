plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

repositories {
    maven { url "https://reposilite.appleptr16.com/appleptr16" }
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

project.group groupValue
project.version versionValue
publishing {
    repositories {
        maven {
            name 'appleptr16Repo'
            url 'https://reposilite.appleptr16.com/appleptr16'
            credentials(PasswordCredentials)
        }
        maven {
            name "GitHubPackages"
            url "https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}"
            credentials {
                username System.getenv("GITHUB_ACTOR")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication -> project.shadow.component(publication)
        }
    }
}
task publishToAppleptr16 {
    dependsOn(publishAllPublicationsToAppleptr16RepoRepository)
}
task publishToVolt {
    dependsOn(publishAllPublicationsToGitHubPackagesRepository)
}
configurations {
    all {
        resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
        resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
    }
}

dependencies {
    implementation 'io.yml:YmlFileManager:3.1-SNAPSHOT'
    implementation 'apple.utilities:AppleUtilities:1.3-SNAPSHOT'

    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'

    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'

    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.google.code.gson:gson:2.10'
}

shadowJar {
    outputs.upToDateWhen {
        false
    }
    group project.group
    version project.version
    classifier ''
    relocate 'co.aikar.commands', 'com.voltskiya.lib.acf'
    relocate 'co.aikar.locales', 'com.voltskiya.lib.locales'
    destinationDirectory.set(file(serverBuildDir))
}
